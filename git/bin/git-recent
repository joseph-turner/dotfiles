#!/bin/zsh

autoload colors; colors

usage() {
  echo "$fg[blue]usage: (g|git) recent [-n lines] [-l] [-e]$reset_color"
}

while getopts "hn:ler" opt; do
  case $opt in
    h) # help
      usage
      exit 1
      ;;
    n) # number of results
      NUM=$OPTARG
      ;;
    l) # list mode (non-interactive)
      INTERACTIVE=0
      ;;
    \*)
      echo "Nope"
      exit 1
      ;;
    \?)
      usage
      exit 1
      ;;
  esac
done
shift $((OPTIND -1))

NUM=${NUM-9} # default to 9 lines
INTERACTIVE=${INTERACTIVE-1} # default to interactive

BRANCHES=(
  $(git reflog |
    egrep -io "moving from ([^[:space:]]+)" |
    awk '{ print $3 }' | # extract 3rd column
    awk ' !x[$0]++' | # Removes duplicates. See http://stackoverflow.com/questions/11532157
    egrep -v '^[a-f0-9]{40}$' | # remove hash results
    while read line; do # verify existence
      git rev-parse --verify "$line" &>/dev/null && echo "$line"
    done |
    head -n "$NUM"
  )
)

[ $# -eq 1 ] && git co "${BRANCHES[$1]}" && exit 0

if [[ $INTERACTIVE = '1' ]]; then
  PS3=$'\nChoose a branch: '

  select d in "${BRANCHES[@]}"; do
    test -n "$d" && break;
    echo ">>> Invalid Selection";
  done

  git co "$d"
else
  for i in {1..$#BRANCHES}; do
    printf '%s\n' "$i=${BRANCHES[i]}"
  done
fi
